__PAGE_SIZE = 0x1000 ;
__LARGE_PAGE_SIZE = 0x200000 ;
ENTRY(_start)
SECTIONS
{
 .text : AT (ADDR(.text) & 0x0fffffff)
 {
  . = ALIGN(__PAGE_SIZE);
  __text_user_start = . ;
  *(.text.user)
  . = ALIGN(__PAGE_SIZE);
  __text_user_end = . ;
  *(.text)
  *(.text.*)
  *(.stub)
  . = ALIGN(__LARGE_PAGE_SIZE);
 } =0xCC
 _etext = . ;
 PROVIDE (etext = .) ;
 . = ALIGN(__LARGE_PAGE_SIZE);
 __rodata_start = . ;
 .rodata.hotpatch :
 {
  __rodata_hotpatch_start = . ;
  *(.rodata.hotpatch)
  __rodata_hotpatch_end = . ;
 }
 .rodata :
 {
  *(.rodata)
  *(.rodata.*)
 }
 . = ALIGN(__LARGE_PAGE_SIZE);
 __data_start = . ;
 .data :
 {
  *(.data)
 }
 . = ALIGN(64);
 .data.cacheline_aligned :
 {
  *(.data.cacheline_aligned)
 }
 . = ALIGN(64);
 .data.read_mostly :
 {
  *(.data.read_mostly)
 }
 . = ALIGN(64);
 _edata = . ;
 PROVIDE (edata = .) ;
 __bss_start = . ;
 .bss :
 {
  *(.bss)
  *(.bss.*)
  *(COMMON)
  . = ALIGN(__LARGE_PAGE_SIZE);
 }
 . = ALIGN(__PAGE_SIZE);
 __kernel_end = . ;
 _end = . ;
 PROVIDE (end = .) ;
 .note.netbsd.ident :
 {
  KEEP(*(.note.netbsd.ident));
 }
}
